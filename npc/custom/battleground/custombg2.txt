prontera,163,155,5	script	bg_pvp#random	733,{

	mes "fuck you";
	close;
	
OnInit:
	.minplayer2start = 1;      // minimum players to start (ex. if 3vs3, set to 3)
	.eventlasting = 20*60;  // event duration before auto-reset (20 minutes * seconds)
	setarray .rewarditem[0],   // rewards for the winning team:
		501, 10; // <item>,<amount>

	waitingroom "bg_pvp", .minplayer2start *2 +1, strnpcinfo(0) +"::OnStart", .minplayer2start *2;
	end;
OnStart:
	if ( getwaitingroomstate(0) < .minplayer2start *2 ) end;
	getmapxy .@map$, .@x, .@y, 1;
	warpwaitingpc .@map$, .@x, .@y, .minplayer2start *2;
	.red = createbgid( "guild_vs3", 13,50, strnpcinfo(0)+"::OnRedQuit", strnpcinfo(0)+"::OnRedDead" );
	.blue = createbgid( "guild_vs3", 86,50, strnpcinfo(0)+"::OnBlueQuit", strnpcinfo(0)+"::OnBlueDead" );
	for ( .@i = 0; .@i < $@warpwaitingpcnum; .@i++ )
		setbgid ( .@i % 2 )? .red : .blue, $@warpwaitingpc[.@i];
	delwaitingroom strnpcinfo(0);
	bg_warp .red, "guild_vs3", 13,50;
	bg_warp .blue, "guild_vs3", 86,50;
	.score[1] = .score[2] = .minplayer2start;
	bg_updatescore "guild_vs3", .score[1], .score[2];
	sleep .eventlasting * 1000;
	if ( .score[1] > .score[2] ) {
		mapannounce "guild_vs3", "- Red Team is victorious! -", bc_map;
		callsub L_Reward, .red;
	}
	else if ( .score[1] < .score[2] ) {
		mapannounce "guild_vs3", "- Blue Team is victorious! -", bc_map;
		callsub L_Reward, .blue;
	}
	else
		mapannounce "guild_vs3", "- The match has ended in a draw! -", bc_map;
	mapwarp "guild_vs3","prontera",152,178;
	bg_destroy .red;
	bg_destroy .blue;
	waitingroom "bg_pvp", .minplayer2start *2 +1, strnpcinfo(0) +"::OnStart", .minplayer2start *2;
	end;
L_Reward:
	getbgusers getarg(0);
	for ( .@i = 0; .@i < $@arenamembersnum; .@i++ )
		getitem .rewarditem[0], .rewarditem[1], $@arenamembers[.@i];
	return;
OnRedDead:  callsub L_Dead, 1;
OnBlueDead: callsub L_Dead, 2;
L_Dead:
	warp "Save", 0,0;
	.score[ getarg(0) ]--;
	bg_updatescore "guild_vs3", .score[1], .score[2];
	bg_leave;
	if ( !.score[ getarg(0) ] )
		awake strnpcinfo(0);
	sleep2 1250;
	percentheal 100,100;
	end;
OnRedQuit:  callsub L_Quit, 1;
OnBlueQuit: callsub L_Quit, 2;
L_Quit:
	.score[ getarg(0) ]--;
	bg_updatescore "guild_vs3", .score[1], .score[2];
	if ( !.score[ getarg(0) ] )
		awake strnpcinfo(0);
	percentheal 100, 100;
	end;
}

guild_vs3	mapflag	battleground	2
guild_vs3	mapflag	nosave	SavePoint
guild_vs3	mapflag	nowarp
guild_vs3	mapflag	nowarpto
guild_vs3	mapflag	noteleport
guild_vs3	mapflag	nomemo
guild_vs3	mapflag	nopenalty
guild_vs3	mapflag	nobranch
guild_vs3	mapflag	noicewall