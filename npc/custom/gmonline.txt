prontera,156,225,5	script	Mithos Yggdrasill	811,{

	mes "Ragnarok Tales GM list : "+( .admincount + .gmcount );
	mes "-----------------------------";
	for ( .@i = 0; .@i < .admincount; .@i++ ) {
		mes "^ff0000GM "+ .admin$[.@i]+ "^000000 ["+( ( isloggedin( getcharid( 3, .admin$[.@i] ) ) )? ( ( checkidle( .admin$[.@i] ) > .idletime )? "^eb43edIdle^000000" : "^2dd50cOnline^000000" ) : "^b5b5b5Offline^000000" )+ "]";
	}
	for ( .@i = 0; .@i < .gmcount; .@i++ ) {
		mes "^ff8c00GM "+ .gm$[.@i]+ "^000000 ["+( ( isloggedin( getcharid( 3, .gm$[.@i] ) ) )? ( ( checkidle( .gm$[.@i] ) > .idletime )? "^eb43edIdle^000000" : "^2dd50cOnline^000000" ) : "^b5b5b5Offline^000000" )+ "]";
	}
	mes gettimestr("%I:%M%p ",10) + gettime(5) + gettimestr(" %B ",15) + gettime(7);
	close;

OnInit:

	setarray .Message$," <3 ",
				"Please be reminded that GMs will NEVER ask you for your password.",
				"You can help our server by voting for us! Visit www.ragnaroktales.com to vote!",
				"Welcome to Ragnarok Tales!",
				" . . . ";

	.idletime = 120; // idle for 60 seconds = idle status
	setarray .admin$, "Snaehild";
	setarray .gm$, "FryD", "Raziel", "Lemz";

	.admincount = getarraysize( .admin$ );
	.gmcount = getarraysize( .gm$ );

	startnpctimer;
	npcspeed 200;
	end;
 
	OnTimer15000:
	npcwalkto 138+rand(5),208;
	npctalk .Message$[ rand( getarraysize( .Message$ ) ) ];
	end;
 
	OnTimer30000:
	npcwalkto 127+rand(5),199;
	end;
 
	OnTimer45000:
	npcwalkto 150,185+rand(5);
	end;
 
	OnTimer60000:
	npcwalkto 156+rand(5),176;
	end;

	OnTimer75000:
	npcwalkto 175,201+rand(5);
	npctalk .Message$[ rand( getarraysize( .Message$ ) ) ];
	end;

	OnTimer90000:
	npcwalkto 156+rand(5),225;
	end;
 
	OnTimer105000:
	stopnpctimer;
	initnpctimer;
	end;
}