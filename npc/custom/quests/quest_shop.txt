//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5027,1,0,0,2252,1,1036,400,7001,50,4052,1,7539,1); //mage hat
	Add(1,5421,1,100000000,0,2286,1,7122,60,7539,1); //ifrit ears
	Add(1,5370,1,0,0,5302,1,981,5,7539,1); //orchid hairband
	Add(1,5237,1,0,0,2227,1,7038,500,975,1,982,1,7539,1); //pink beanie
	Add(1,5412,1,0,0,530,20,7035,10,7539,1); //lollipop
	Add(1,5191,1,0,0,2208,1,983,1,7539,1); //black ribbon 
	Add(1,5182,1,0,0,5008,10,5118,1,1060,100,2210,1,7539,1); //puppy hat
	Add(1,2281,1,5000,0,998,20,707,1,7539,1); //opera masque
	Add(1,5228,1,0,0,1038,600,7048,40,982,1,7539,1); //gray deviruchi hat
	Add(1,5288,1,0,0,5047,1,2276,1,7539,2); //red glasses
	Add(1,5091,1,20000,0,10016,1,714,1,969,3,7539,1); //decorative bell
	Add(1,5431,1,50000000,0,5283,1,916,50,2717,1,7539,1); //chicken hat
	Add(1,5051,1,0,0,7047,100,7539,1); //pussy cat bell
	Add(1,5100,1,0,0,2272,1,7451,4,7433,3,690,10,7539,1); //sales banner
	Add(1,5138,1,0,0,5123,1,5027,1,7337,50,981,1,7539,1); //magic eye
	Add(1,5271,1,0,0,2254,1,723,1,7301,20,999,50,978,1,7539,1); //phoenix crown 
	Add(1,5256,1,0,0,7510,1,2210,1,7063,50,7539,1); //valkyrie featherband
	Add(1,5057,1,10000,0,2213,1,983,1,914,200,7539,1); //black cat ears
	Add(1,5070,1,0,0,7216,300,7097,300,2211,1,982,1,7539,1); //hot blooded headband
	Add(1,5077,1,0,0,2278,1,975,1,7539,1); //tulip hairpin
	Add(1,5307,1,0,0,2275,5,514,2,979,1,634,1,619,20,1027,20,7539,1); //carmen miranda hat
	Add(1,5131,1,0,0,5128,1,969,10,978,1,985,27,7539,1); //close helm
	Add(1,5153,1,0,0,10011,1,7539,1); //angeling hairpin
	Add(1,5240,1,0,0,2252,1,1036,400,7001,50,4052,1,982,1,983,1,7539,1); //brown mage hat
	Add(1,5312,1,0,0,2261,1,983,1,2280,1,2627,1,969,2,7539,1); //ayatoya king hat
	Add(1,5023,1,0,0,1059,150,907,100,978,1,7539,1); //parcel hat
	Add(1,5069,1,0,0,1022,99); //kitsune mask
	Add(1,5076,1,0,0,2227,1,7038,500,7539,1); //beanie
	Add(1,5169,1,0,0,706,1,1019,500,7200,10,1049,20,7165,500,1054,2,1024,100,7539,1); //bride mask
	Add(1,5029,1,0,0,7068,300,7033,850,1015,1,7539,1); //spore hat
	Add(1,5075,1,0,0,2248,1,7030,108,7194,108,7120,4,7539,1); //cow boy hat
	Add(1,5424,1,0,0,980,1,620,1,605,50,985,200,7539,1); //fanta orange can hat
	Add(1,5047,1,0,0,2271,1,975,1,7539,1); //fashion glasses
	Add(1,5807,1,0,0,5416,1,2211,2,949,100,7539,1); //october fest cap
	Add(1,5365,1,0,0,574,150,7031,150,7539,1); //mangolia hat
	Add(1,5200,1,0,0,5016,1,981,1,7317,20,7539,1); //coppola
	Add(1,5079,1,0,0,2294,1,7220,400); //x-hairpin
	Add(1,5409,1,0,0,5075,1,7101,5,981,10,7539,1); //purple cowboy hat
	Add(1,5299,1,0,0,7441,20,949,50,916,15,7115,10,7440,20,7063,10,7200,10,2214,1,7539,1); //pagdayaw
	Add(1,5381,1,0,0,5335,1,2236,1,7539,1); //santa poring hat
	Add(1,5232,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,975,1,7539,1); //pink drooping cat
	Add(1,5231,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,983,1,7539,1); //brown drooping cat
	Add(1,5344,1,0,0,5137,1,7038,20,1060,25,7539,1); //bennit doll
	Add(1,5212,1,0,0,1820,2,5011,1,2651,1,4125,1,7539,1); //satellite hairband
	Add(1,5187,1,0,0,5085,1,5091,1,5117,2,978,1,975,1,7539,1); //twin red ribbon
	Add(1,2293,1,0,0,1049,4,7539,1); //pretend murder
	Add(1,5038,1,0,0,1038,600,7048,40,7539,1); //deviruchi hat
	Add(1,5062,1,0,0,2280,1,7197,300,7150,300,7539,1); //straw hat
	Add(1,5403,1,0,0,1023,1,544,5,579,1,7539,1); //fish in mouth
	Add(1,5192,1,0,0,2208,1,976,1,7539,1); //yellow ribbon
	Add(1,5170,1,0,0,5172,1,7063,100,982,1,7539,3); //feather berret
	Add(1,5274,1,0,0,2261,1,914,200,980,2,975,1,7539,4); //shiny wig
	Add(1,5374,1,0,0,6004,5,5160,1,923,25,7539,1); //gigantic magestic goat
	Add(1,5275,1,0,0,2261,1,914,200,975,1,976,1,981,1,7539,4); //marvelous wig
	Add(1,5423,1,5000000,0,736,300,5074,1,994,1,7539,8); // i love china

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1