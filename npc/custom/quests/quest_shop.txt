//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5405,1,0,0,7441,45,949,80,916,100,7539,1); //filir hat
	Add(1,5129,1,0,0,604,1,916,1,7539,1); //birds nest
	Add(1,5004,1,0,0,701,5,7539,1); //oxygen mask
	Add(1,5108,1,5000,0,7301,1887,5114,1,611,10,7539,1); //detective cap
	Add(1,5135,1,0,0,2295,1,969,1,975,1,999,5,7539,1); //cyclopes eye
	Add(1,5396,1,0,0,5058,1,980,1,1022,53,7539,1); //jasper crest
	Add(1,5294,1,0,0,7019,5,7567,50,7539,1); //wisper mask
	Add(1,5372,1,0,0,5099,1,5230,1,2747,5,982,15,7539,1); //koneko hat
	Add(1,5069,1,0,0,1022,99); //kitsune mask
	Add(1,5255,1,0,0,5153,1,741,10,5132,1,5074,1,7539,1); //triple poring hat
	Add(1,5056,1,0,0,5079,1,578,100,7539,1); //fruit of love
	Add(1,5152,1,0,0,2254,1,724,1,7301,20,999,50,978,1); //chinese crown
	Add(1,2283,1,0,0,724,1,5001,1,949,200,7539,1); //ear muff
	Add(1,5322,1,0,0,10019,1,982,2,7539,1); //scarf
	Add(1,5342,1,0,0,5137,1,5040,1,1020,20,7152,5,7539,1); //sorin doll hat
	Add(1,5154,1,0,0,2202,1,7020,5,7539,1); //father's sunglasses
	Add(1,5293,1,0,0,577,20,7134,1,581,3,7454,2,7455,2,6033,2,6022,2,7539,1); //ramen hat
	Add(1,5433,1,0,0,5185,1,969,2,7539,1); //champions,s wreath
	Add(1,5259,1,0,0,978,1,7561,100,949,500,1026,200,7539,1); //elephant hat
	Add(1,5140,1,0,0,5085,1,2244,1,5191,1,7166,50,7539,1); //charming ribbon
	Add(1,5138,1,0,0,5123,1,5027,1,7337,50,981,1,7539,1); //magic eye
	Add(1,5235,1,0,0,2227,1,7038,500,982,1,983,1,7539,1); //brown beanie
	Add(1,5321,1,0,0,2275,2,2211,2,7215,4,7217,20,7539,1); //indonasina bandana
	Add(1,5086,1,0,0,5024,1,539,30,660,3,7539,1); //alarm mask
	Add(1,5430,1,50000000,0,5414,1,7135,10,7451,4,7539,1); //promethean crown
	Add(1,5149,1,0,0,2234,1,714,5,2611,25,7539,1); //silver tiera
	Add(1,5054,1,0,0,2252,1,1054,450,943,1200,7539,1); //assasin mask
	Add(1,5068,1,20000,0,2255,1,2286,1,7539,1); //evil wing ears
	Add(1,2293,1,0,0,1049,4,7539,1); //pretend murder
	Add(1,5209,1,0,0,5147,1,976,1,7539,1); //love dad cap
	Add(1,5300,1,0,0,7106,2,2246,1,7263,1,7539,1); //bullock helm
	Add(1,5032,1,0,0,1059,250,2221,1,2227,1,7063,600,7539,1); //sunday hat
	Add(1,5153,1,0,0,10011,1,7539,1); //angeling hairpin
	Add(1,5082,1,0,0,921,300,7539,1); //decorative mushroom
	Add(1,5381,1,0,0,5335,1,2236,1,7539,1); //santa poring hat
	Add(1,5323,1,0,0,5081,3,7754,1,2128,1,7539,1); //misstrance crown
	Add(1,5202,1,0,0,5035,1,1062,100,535,80,7539,1); //pumkin hat
	Add(1,5058,1,0,0,2233,1,983,1,7206,300,7030,1,7539,1); //drooping cat
	Add(1,5241,1,0,0,2252,1,1036,400,7001,50,4052,1,978,1,7539,1); //blue mage hat
	Add(1,5233,1,0,0,2233,1,983,1,7206,300,7030,1,978,1,7539,1); //blue drooping cat
	Add(1,5243,1,0,0,975,1,976,1,914,100,7441,150,7065,25,2283,1,7539,1); //shafka
	Add(1,5363,1,0,0,5120,1,2261,1,983,20,7539,1); //abyssmal knight helm
	Add(1,5151,1,0,0,5001,1,978,1,991,200,7539,1); //note headphone
	Add(1,5230,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,7539,1); //gray drooping cat
	Add(1,5197,1,0,0,2208,1,982,1,7539,1); //white ribbon
	Add(1,5243,1,0,0,7446,1,2339,10,1059,70,7539,1);//shafka
	Add(1,5320,1,0,0,5205,1,1009,50,2111,1,7539,1); //champion wreath
	Add(1,5034,1,0,0,2233,1,746,20,7539,1); //bulb band
	Add(1,5173,1,0,0,7445,7,7446,7,7447,7,7478,7,945,300,983,1,733,5,2221,1,7539,1); //magistrate hat
	Add(1,5067,1,0,0,5062,1,952,50,1907,1,7539,1); //sumbrero
	Add(1,5377,1,100000000,0,2268,1,1040,30,1629,1,7539,7); //gentleman pipe
	Add(1,5423,1,5000000,0,736,300,5074,1,994,1,7539,8); // i love china
	Add(1,5361,1,0,0,7216,200,7539,7); //gangster scarf
	Add(1,5171,1,0,0,4219,1,4114,1,4177,1,4259,1,4212,1,4073,1,4112,1,4081,1,4251,1,4166,1,7511,1000,7563,1000,7539,7); //valkyrie helm
	Add(1,5376,1,0,0,641,1,2210,1,7063,60,983,4,7539,3); //flying evil wing	

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1