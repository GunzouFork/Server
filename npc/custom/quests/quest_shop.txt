//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5269,1,0,0,5203,4,7063,60,2210,1,7539,1); //flapping angel wing
	Add(1,5392,1,100000,0,721,5,7216,25,7539,1); //louyang hat
	Add(1,5062,1,0,0,2280,1,7197,300,7150,300,7539,1); //straw hat
	Add(1,5388,1,0,0,1048,50,926,50,7539,1); //snake hat 
	Add(1,5026,1,0,0,1036,450,949,330,539,120,982,1,7539,1); //chef hat
	Add(1,5075,1,0,0,2248,1,7030,108,7194,108,7120,4,7539,1); //cow boy hat
	Add(1,5174,1,0,0,7445,7,7446,7,7447,7,7448,7,945,300,975,1,733,5,7166,50,7539,1); //ayam
	Add(1,5426,1,0,0,5027,1,976,5,7539,1); //karada
	Add(1,5094,1,0,0,968,100,2299,1,1124,1,931,1000,7539,2); //orc hero helm
	Add(1,2240,1,0,0,1040,2,982,2,949,4,7457,1,7539,1); //grandpa beared
	Add(1,5214,1,0,0,976,1,1060,300,1022,100,5008,5,7539,1); //moonlight flower hat
	Add(1,5258,1,0,0,2261,1,914,200,975,1,976,1,978,1,7539,1); //afro wig
	Add(1,5211,1,0,0,5045,1,5083,1,7063,40,7553,1,982,1,975,1,7539,2); //dress hat
	Add(1,5337,1,0,0,5100,1,7112,25,2617,1,7539,1); //party recruiter hat
	Add(1,5029,1,0,0,7068,300,7033,850,1015,1,7539,1); //spore hat
	Add(1,5381,1,0,0,5335,1,2236,1,7539,1); //santa poring hat
	Add(1,5427,1,0,0,5288,1,12078,1,7521,1,12183,1,7539,1); //black tea kochakaden hat
	Add(1,5344,1,0,0,5137,1,7038,20,1060,25,7539,1); //bennit doll
	Add(1,5117,1,50000,0,731,10,748,2,982,1,7539,1); //mystic rose
	Add(1,5403,1,0,0,1023,1,544,5,579,1,7539,1); //fish in mouth
	Add(1,5191,1,0,0,2208,1,983,1,7539,1); //black ribbon 
	Add(1,5153,1,0,0,10011,1,7539,1); //angeling hairpin
	Add(1,5270,1,0,0,7198,200,7097,40,7539,1); //autum leaves
	Add(1,5294,1,0,0,7019,5,7567,50,7539,1); //wisper mask
	Add(1,5209,1,0,0,5147,1,976,1,7539,1); //love dad cap
	Add(1,5801,1,0,0,5083,1,2700,12,7566,12,7562,12,2357,3,7539,1); //red tailed ribbon
	Add(1,5307,1,0,0,2275,5,514,2,979,1,634,1,619,20,1027,20,7539,1); //carmen miranda hat
	Add(1,5023,1,0,0,1059,150,907,100,978,1,7539,1); //parcel hat
	Add(1,5259,1,0,0,978,1,7561,100,949,500,1026,200,7539,1); //elephant hat
	Add(1,5130,1,0,0,1048,100,7564,150,517,30); //lion mask
	Add(1,5107,1,0,0,519,50,7031,50,548,50,539,50,7539,1); //crunch toast
	Add(1,5185,1,0,0,5061,1,712,100,7194,150,7298,100,520,50,521,50,7539,1); //laurel wreath
	Add(1,5416,1,0,0,746,20,7097,12,587,20,2717,1,7539,1); //bear hat
	Add(1,5192,1,0,0,2208,1,976,1,7539,1); //yellow ribbon
	Add(1,5083,1,0,0,2244,1,2209,1,10007,1,7539,1); //red ribbon
	Add(1,5431,1,50000000,0,5283,1,916,50,2717,1,7539,1); //chicken hat
	Add(1,5129,1,0,0,604,1,916,1,7539,1); //birds nest
	Add(1,5375,1,0,0,5094,1,10018,2,968,5,7440,30,7539,2); //orc boss hat 
	Add(1,5100,1,0,0,2272,1,7451,4,7433,3,690,10,7539,1); //sales banner
	Add(1,5807,1,0,0,5416,1,2211,2,949,100,7539,1); //october fest cap
	Add(1,5243,1,0,0,975,1,976,1,914,100,7441,150,7065,25,2283,1,7539,1); //shafka
	Add(1,5414,1,100000000,0,7300,9,1068,4,7098,2,7539,1); //campfire hat
	Add(1,5143,1,0,0,5090,1,1020,300,975,1,982,1,7539,1); //kabuki mask
	Add(1,5183,1,0,0,604,100,916,100,7115,100,7539,1); //bird nest hat
	Add(1,5154,1,0,0,2202,1,7020,5,7539,1); //father's sunglasses
	Add(1,5256,1,0,0,7510,1,2210,1,7063,50,7539,1); //valkyrie featherband
	Add(1,5365,1,0,0,574,150,7031,150,7539,1); //mangolia hat
	Add(1,5069,1,0,0,1022,99); //kitsune mask
	Add(1,5384,1,0,0,5390,1,975,1,7539,1); //twin pom santa
	Add(1,5031,1,0,0,7013,1200,7539,1); //mine hat
	Add(1,5377,1,100000000,0,2268,1,1040,30,1629,1,7539,7); //gentleman pipe
	Add(1,5273,1,0,0,2261,1,914,200,982,1,975,1,979,1,7539,4); //Happy Wig
	Add(1,5361,1,0,0,7216,200,7539,7); //gangster scarf
	Add(1,5305,1,0,0,1214,5,969,10,7799,5,7539,6); //pirate dagger
	Add(1,5376,1,0,0,641,1,2210,1,7063,60,983,4,7539,3); //flying evil wing

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1