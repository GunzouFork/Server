//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,30029,1,0,0,5027,1,1062,100,7225,100,7609,50,981,5,7539,5); //Witch's Pumpkin Hat
	Add(1,5098,1,0,0,1029,10,2297,5,7267,50,7539,1); //tiger mask
	Add(1,5059,1,0,0,5030,1,7213,100,7217,100,7161,300,7539,1); //teddybear hat
	Add(1,5206,1,0,0,2269,1,982,1,7539,1); //romantic white flower
	Add(1,5807,1,0,0,5416,1,2211,2,949,100,7539,1); //october fest cap
	Add(1,5375,1,0,0,5094,1,10018,2,968,5,7440,30,7539,2); //orc boss hat 
	Add(1,5131,1,0,0,5128,1,969,10,978,1,985,27,7539,1); //close helm
	Add(1,5137,1,0,0,5141,5,7047,100,661,1,7539,2); //alice doll
	Add(1,5414,1,100000000,0,7300,9,1068,4,7098,2,7539,1); //campfire hat
	Add(1,5404,1,0,0,5187,1,1034,70,7441,10,978,10,7539,1); //blue ribbon 
	Add(1,5273,1,0,0,2261,1,914,200,982,1,975,1,979,1,7539,4); //Happy Wig
	Add(1,5047,1,0,0,2271,1,975,1,7539,1); //fashion glasses
	Add(1,5156,1,0,0,2265,10,982,1,7539,1); //fater's mask
	Add(1,5137,1,0,0,5141,5,7047,100,661,1,7539,2); //alice doll
	Add(1,5026,1,0,0,1036,450,949,330,539,120,982,1,7539,1); //chef hat
	Add(1,5071,1,10000,0,5010,1,5049,1,7101,10,7539,1); //indian headband
	Add(1,5062,1,0,0,2280,1,7197,300,7150,300,7539,1); //straw hat
	Add(1,5130,1,0,0,1048,100,7564,150,517,30); //lion mask
	Add(1,2278,1,0,0,705,10,909,10,914,10,7539,1); //mr. smile
	Add(1,5016,1,0,0,1030,10,0,0,0,0,0,0); //boys cap
	Add(1,5146,1,0,0,978,1,949,500,1019,1,7539,1); //elephant hat
	Add(1,5427,1,0,0,5288,1,12078,1,7521,1,12183,1,7539,1); //black tea kochakaden hat
	Add(1,5175,1,100000,0,7111,100,938,99,983,1,7539,1); //censor bar
	Add(1,5143,1,0,0,5090,1,1020,300,975,1,982,1,7539,1); //kabuki mask
	Add(1,5393,1,777777,0,2285,5,994,10,7097,25,7539,6); //vallentine hat
	Add(1,5410,1,50000000,0,7188,40,7111,20,7539,1); //brown paperbag hat
	Add(1,5252,1,0,0,5058,1,1022,250,7267,100,7539,1); //drooping 9tails
	Add(1,5325,1,0,0,5102,1,7355,12,7095,4,7317,2,7539,8); //robo eye
	Add(1,5039,1,0,0,7030,50,978,1,5015,1,7539,1); //rainbow eggshell
	Add(1,5312,1,0,0,2261,1,983,1,2280,1,2627,1,969,2,7539,1); //ayatoya king hat
	Add(1,5411,1,0,0,5388,1,982,5,7263,2,978,2,7539,1); //white snake hat
	Add(1,5109,50000,0,0,10015,1,10007,1,5023,1,975,1,7539,1); //red bonnet
	Add(1,5287,1,0,0,5062,1,5348,1,978,1,7539,1); //vacation hat
	Add(1,5390,1,0,0,2236,2,978,1,1034,50,2294,2,7539,1); //blue x-mass cheer
	Add(1,2237,1,0,0,1020,100,983,1,2241,1,7539,1); //bandit beard
	Add(1,5382,1,0,0,969,8,10016,2,5083,1,7539,1); //bell ribbon
	Add(1,5269,1,0,0,5203,4,7063,60,2210,1,7539,1); //flapping angel wing
	Add(1,5142,1,0,0,5017,1,2229,1,5014,1,7209,100,7123,100,1036,100,979,1,7539,1); //cresent helm
	Add(1,5207,1,0,0,2282,5,2254,2,969,1,7063,15,7539,1); //angel blessing
	Add(1,5060,1,0,0,2236,1,7151,100,7111,100,7539,1); //party hat
	Add(1,5336,1,0,0,5100,1,7112,25,714,1,7539,1); //guildmen recuiter hat 
	Add(1,5288,1,0,0,5047,1,2276,1,7539,2); //red glasses
	Add(1,5337,1,0,0,5100,1,7112,25,2617,1,7539,1); //party recruiter hat
	Add(1,5236,1,0,0,2227,1,7038,500,978,1,7539,1); //blue beanie
	Add(1,5025,1,0,0,2229,1,2254,1,7036,5,7539,1); //angelhelm
	Add(1,5811,1,0,0,985,100,2241,10,7539,1); //santa beared
	Add(1,5261,1,0,0,5164,1,714,25,2611,50,7799,4,7539,1); //silver tiara
	Add(1,5369,1,0,0,5191,6,7205,25,558,3,2210,1,7441,5,7539,1); //dark wing
	Add(1,5415,1,0,0,5255,1,5024,1,7539,1); //poring cake hat
	Add(1,5195,1,0,0,2208,1,975,1,7539,1); //red ribbond
	Add(1,5805,1,0,0,5161,1,7539,15); //victorious coronet
	Add(1,5334,1,0,0,5132,1,1971,20,7093,4,2659,1,7539,1); //flapping angeling
	Add(1,5121,1,0,0,7263,1,660,1,7099,30,7315,369,7539,1); //zealotus mask
	Add(1,5389,1,0,0,5043,5,969,10,7568,50,7539,8); //angel spirit
	Add(1,5299,1,0,0,7441,20,949,50,916,15,7115,10,7440,20,7063,10,7200,10,2214,1,7539,1); //pagdayaw
	Add(1,5302,1,0,0,7553,2,982,1,7771,1,7539,1); //water liliy hat

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1