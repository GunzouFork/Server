//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5352,1,0,0,5035,1,979,1,938,100,7539,1); //poporing cap
	Add(1,5077,1,0,0,2278,1,975,1,7539,1); //tulip hairpin
	Add(1,5182,1,0,0,5008,10,5118,1,1060,100,2210,1,7539,1); //puppy hat
	Add(1,5040,1,0,0,7047,100,7539,1); //blush
	Add(1,5420,1,300000000,0,5430,2,5421,1,7539,1); //ifrit mask
	Add(1,5360,1,0,0,5057,1,2747,2,5161,1,7539,1); //wickebine ears
	Add(1,5004,1,0,0,701,5,7539,1); //oxygen mask
	Add(1,5315,1,0,0,2276,1,5135,1,7094,50,7353,50,7539,1); //observer
	Add(1,5293,1,0,0,577,20,7134,1,581,3,7454,2,7455,2,6033,2,6022,2,7539,1); //ramen hat
	Add(1,5099,1,0,0,7267,300,7171,300,1029,5,7539,1); //neko mini
	Add(1,5133,1,0,0,5160,1,7106,6,7107,80,7539,1); //sheep hat
	Add(1,5316,1,0,0,2226,1,622,10,7539,1); //umberalla hat
	Add(1,5345,1,0,0,5137,1,5195,2,1060,45,7539,1); //w doll hat
	Add(1,5297,1,0,0,5257,1,1472,2,5158,2,7063,40,7539,1); //soul wing
	Add(1,5229,1,0,0,1038,600,7048,40,982,1,983,1,7539,1); //brown deviruchi hat
	Add(1,5086,1,0,0,5024,1,539,30,660,3,7539,1); //alarm mask
	Add(1,5355,1,0,0,7166,100,1059,200,7539,1); //selendang
	Add(1,5237,1,0,0,2227,1,7038,500,975,1,982,1,7539,1); //pink beanie
	Add(1,5027,1,0,0,2252,1,1036,400,7001,50,4052,1,7539,1); //mage hat
	Add(1,2272,1,911000,0,1019,50,983,1,7539,1); //stop post
	Add(1,5373,1,0,0,2255,1,7566,3,7511,60,5104,1,7799,4,7023,20,7539,3); //dark rangis helm
	Add(1,2237,1,0,0,1020,100,983,1,2241,1,7539,1); //bandit beard
	Add(1,5405,1,0,0,7441,45,949,80,916,100,7539,1); //filir hat
	Add(1,2292,1,2000,0,999,50,7539,1); //welding mask
	Add(1,5288,1,0,0,5047,1,2276,1,7539,2); //red glasses
	Add(1,5412,1,0,0,530,20,7035,10,7539,1); //lollipop
	Add(1,5303,1,0,0,712,100,710,5,629,5,10009,1,7539,1); //flower crown
	Add(1,5292,1,0,0,7266,2,1035,4,10001,1,7539,1); //dragon skull
	Add(1,5081,1,40000,0,2249,1,714,1,969,3,7539,1); //mistress crown
	Add(1,5132,1,0,0,5035,1,5074,1,714,1,999,5,7539,1); //angeling hat
	Add(1,5317,1,0,0,5120,1,2290,1,938,4,7539,1); //Fisherman's Hat
	Add(1,5022,1,0,0,7086,1,969,10,999,40,1003,50,984,2,7539,1); //hat of the sun
	Add(1,5449,1,0,0,521,200,1019,300,7539,1); //angry solo hat
	Add(1,5370,1,0,0,5302,1,981,5,7539,1); //orchid hairband
	Add(1,5247,1,0,0,1039,60,923,25,2255,1,7539,1); //satanic helm
	Add(1,5359,1,0,0,2284,1,5019,1,975,1,7539,7); //ship captain hat
	Add(1,5034,1,0,0,2233,1,746,20,7539,1); //bulb band
	Add(1,5234,1,0,0,2233,1,983,1,7206,300,7030,1,976,1,7539,1); //yellow drooping cat
	Add(1,5395,1,0,0,5045,2,7205,150,983,1,1059,100,7539,1); //tiraya bonet
	Add(1,5047,1,0,0,2271,1,975,1,7539,1); //fashion glasses
	Add(1,5322,1,0,0,10019,1,982,2,7539,1); //scarf
	Add(1,5241,1,0,0,2252,1,1036,400,7001,50,4052,1,978,1,7539,1); //blue mage hat
	Add(1,5103,1,0,0,5351,1,10011,1,7539,1); //sunflower ahirpin
	Add(1,5409,1,0,0,5075,1,7101,5,981,10,7539,1); //purple cowboy hat
	Add(1,5079,1,0,0,2294,1,7220,400); //x-hairpin
	Add(1,5238,1,0,0,2252,1,1036,400,7001,50,4052,1,975,1,7539,1); //red mage hat
	Add(1,5198,1,0,0,5058,1,2214,1,949,500,914,200,622,50,1974,1,7539,1); //evolve drooping bunny
	Add(1,5422,1,500000,0,1550,3,1006,20,7539,1); //linguistic book hat
	Add(1,5346,1,0,0,5100,1,7112,25,558,1,7539,1); //gf recruter hat
	Add(1,5052,1,0,0,2211,1,978,1,7003,300,7539,1); //blue hairband
	Add(1,5800,1,0,0,5040,1,7170,1,2371,1,7539,6); //blush of groom
	Add(1,5171,1,0,0,4219,1,4114,1,4177,1,4259,1,4212,1,4073,1,4112,1,4081,1,4251,1,4166,1,7511,1000,7563,1000,7539,7); //valkyrie helm
	Add(1,5393,1,777777,0,2285,5,994,10,7097,25,7539,6); //vallentine hat
	Add(1,5423,1,5000000,0,736,300,5074,1,994,1,7539,8); // i love china
	Add(1,18507,1,0,0,1040,200,993,20,997,20,969,5,1005,1,2286,1,7539,7); //slotted elven ears

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1