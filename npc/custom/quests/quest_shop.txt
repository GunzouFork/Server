//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5192,1,0,0,2208,1,976,1,7539,1); //yellow ribbon
	Add(1,5341,1,80000000,0,5137,1,2276,1,1060,25,7539,1); //glaris doll hat
	Add(1,5292,1,0,0,7266,2,1035,4,10001,1,7539,1); //dragon skull
	Add(1,5202,1,0,0,5035,1,1062,100,535,80,7539,1); //pumkin hat
	Add(1,5372,1,0,0,5099,1,5230,1,2747,5,982,15,7539,1); //koneko hat
	Add(1,5208,1,0,0,1550,1,1553,7,1556,7,1554,7,1557,7,1555,7,7015,30,7539,3); //rideword hat
	Add(1,5304,1,0,0,7017,2,2510,1,983,1,7539,1); //cap of blindness
	Add(1,5415,1,0,0,5255,1,5024,1,7539,1); //poring cake hat
	Add(1,5238,1,0,0,2252,1,1036,400,7001,50,4052,1,975,1,7539,1); //red mage hat
	Add(1,5110,1,0,0,526,2,7270,1,941,1,10004,1,7539,1); //baby pacifier
	Add(1,5257,1,0,0,5074,1,2282,1,7064,10,969,5,7539,1); //soul ring
	Add(1,5283,1,0,0,949,200,916,100,7063,20,925,1,7539,1); //chick hat
	Add(1,5336,1,0,0,5100,1,7112,25,714,1,7539,1); //guildmen recuiter hat 
	Add(1,5228,1,0,0,1038,600,7048,40,982,1,7539,1); //gray deviruchi hat
	Add(1,5411,1,0,0,5388,1,982,5,7263,2,978,2,7539,1); //white snake hat
	Add(1,5040,1,0,0,7047,100,7539,1); //blush
	Add(1,5422,1,500000,0,1550,3,1006,20,7539,1); //linguistic book hat
	Add(1,5098,1,0,0,1029,10,2297,5,7267,50,7539,1); //tiger mask
	Add(1,5049,1,0,0,1099,1500,7539,1); //stripped hairband
	Add(1,5361,1,0,0,7216,200,7539,7); //gangster scarf
	Add(1,5300,1,0,0,7106,2,2246,1,7263,1,7539,1); //bullock helm
	Add(1,5363,1,0,0,5120,1,2261,1,983,20,7539,1); //abyssmal knight helm
	Add(1,2296,1,50000,0,2243,1,999,100,7539,1); //binoculars
	Add(1,5069,1,0,0,1022,99); //kitsune mask
	Add(1,5431,1,50000000,0,5283,1,916,50,2717,1,7539,1); //chicken hat
	Add(1,5426,1,0,0,5027,1,976,5,7539,1); //karada
	Add(1,5388,1,0,0,1048,50,926,50,7539,1); //snake hat
	Add(1,5321,1,0,0,2275,2,2211,2,7215,4,7217,20,7539,1); //indonasina bandana
	Add(1,5177,1,0,0,706,1,1019,500,7200,10,1049,500,1053,2,980,1,7539,1); //mythical lion mask
	Add(1,5413,1,0,0,608,20,1752,1,2261,1,978,1,7539,1); //popcorn hat
	Add(1,5318,1,0,0,5035,2,5060,2,741,10,530,50,938,100,7539,1); //poring party hat
	Add(1,5170,1,0,0,5172,1,7063,100,982,1,7539,3); //feather berret
	Add(1,5100,1,0,0,2272,1,7451,4,7433,3,690,10,7539,1); //sales banner
	Add(1,5382,1,0,0,969,8,10016,2,5083,1,7539,1); //bell ribbon
	Add(1,5390,1,0,0,2236,2,978,1,1034,50,2294,2,7539,1); //blue x-mass cheer
	Add(1,5154,1,0,0,2202,1,7020,5,7539,1); //father's sunglasses
	Add(1,5800,1,0,0,5040,1,7170,1,2371,1,7539,6); //blush of groom
	Add(1,5130,1,0,0,1048,100,7564,150,517,30); //lion mask
	Add(1,5173,1,0,0,7445,7,7446,7,7447,7,7478,7,945,300,983,1,733,5,2221,1,7539,1); //magistrate hat
	Add(1,5001,1,0,0,999,40,984,1,970,1,1003,1,7539,1); //headset
	Add(1,5342,1,0,0,5137,1,5040,1,1020,20,7152,5,7539,1); //sorin doll hat
	Add(1,5404,1,0,0,5187,1,1034,70,7441,10,978,10,7539,1); //blue ribbon 
	Add(1,5247,1,0,0,1039,60,923,25,2255,1,7539,1); //satanic helm
	Add(1,5056,1,0,0,5079,1,578,100,7539,1); //fruit of love
	Add(1,5060,1,0,0,2236,1,7151,100,7111,100,7539,1); //party hat
	Add(1,5131,1,0,0,5128,1,969,10,978,1,985,27,7539,1); //close helm
	Add(1,5286,1,0,0,2254,2,7101,200,2210,2,7510,10,7539,4); //pecopeco hairband
	Add(1,5213,1,0,0,2214,1,983,1,7063,30,7539,1); //shiela hairnet
	Add(1,5038,1,0,0,1038,600,7048,40,7539,1); //deviruchi hat
	Add(1,5380,1,0,0,5065,2,7539,1); //fish head hat
	Add(1,5417,1,0,0,4112,1,5299,1,7539,1); //crown parrot
	Add(1,5117,1,50000,0,731,10,748,2,982,1,7539,1); //mystic rose
	Add(1,5237,1,0,0,2227,1,7038,500,975,1,982,1,7539,1); //pink beanie
	Add(1,5073,1,0,0,2285,1,1550,1,7539,1); //model training hat
	Add(1,5333,1,0,0,996,4,12028,1,12032,1,992,10,5011,1,7539,1); //radio anthena

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1