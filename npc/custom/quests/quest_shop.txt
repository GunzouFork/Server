//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5137,1,0,0,5141,5,7047,100,661,1,7539,2); //alice doll
	Add(1,5400,1,0,0,1022,20,1234,2,7539,1); //fox hat
	Add(1,5303,1,0,0,712,100,710,5,629,5,10009,1,7539,1); //flower crown
	Add(1,5154,1,0,0,2202,1,7020,5,7539,1); //father's sunglasses
	Add(1,5396,1,0,0,5058,1,980,1,1022,53,7539,1); //jasper crest
	Add(1,5133,1,0,0,5160,1,7106,6,7107,80,7539,1); //sheep hat
	Add(1,5206,1,0,0,2269,1,982,1,7539,1); //romantic white flower
	Add(1,5322,1,0,0,10019,1,982,2,7539,1); //scarf
	Add(1,5319,1,0,0,5035,1,5074,1,978,1,7539,1); //arch angeling hat
	Add(1,5341,1,80000000,0,5137,1,2276,1,1060,25,7539,1); //glaris doll hat
	Add(1,5321,1,0,0,2275,2,2211,2,7215,4,7217,20,7539,1); //indonasina bandana
	Add(1,5247,1,0,0,1039,60,923,25,2255,1,7539,1); //satanic helm
	Add(1,5109,50000,0,0,10015,1,10007,1,5023,1,975,1,7539,1); //red bonnet
	Add(1,5310,1,0,0,5034,1,1971,2,996,6,7539,1); //shiny electric bulb
	Add(1,5054,1,0,0,2252,1,1054,450,943,1200,7539,1); //assasin mask
	Add(1,5184,1,0,0,5120,5,995,50,983,1,720,50,7539,1); //captains hat
	Add(1,5355,1,0,0,7166,100,1059,200,7539,1); //selendang
	Add(1,5373,1,0,0,2255,1,7566,3,7511,60,5104,1,7799,4,7023,20,7539,3); //dark rangis helm
	Add(1,2296,1,50000,0,2243,1,999,100,7539,1); //binoculars
	Add(1,5426,1,0,0,5027,1,976,5,7539,1); //karada
	Add(1,5300,1,0,0,7106,2,2246,1,7263,1,7539,1); //bullock helm
	Add(1,2280,1,10000,0,1019,120); //sakkat
	Add(1,5174,1,0,0,7445,7,7446,7,7447,7,7448,7,945,300,975,1,733,5,7166,50,7539,1); //ayam
	Add(1,5039,1,0,0,7030,50,978,1,5015,1,7539,1); //rainbow eggshell
	Add(1,5395,1,0,0,5045,2,7205,150,983,1,1059,100,7539,1); //tiraya bonet
	Add(1,5243,1,0,0,975,1,976,1,914,100,7441,150,7065,25,2283,1,7539,1); //shafka
	Add(1,5073,1,0,0,2285,1,1550,1,7539,1); //model training hat
	Add(1,5064,1,0,0,945,600,7030,1,7539,1); //smokie leaf
	Add(1,5313,1,0,0,5124,1,2249,1,5007,1,523,200,7539,1); //diadem
	Add(1,5074,1,20000,0,2286,1,2254,1,7539,1); // angel wing ears
	Add(1,5031,1,0,0,5009,1,5028,1,747,1,999,25,7539,1); //mine hat
	Add(1,5117,1,50000,0,731,10,748,2,982,1,7539,1); //mystic rose
	Add(1,5146,1,0,0,978,1,949,500,1019,1,7539,1); //elephant hat
	Add(1,5416,1,0,0,746,20,7097,12,587,20,2717,1,7539,1); //bear hat
	Add(1,5129,1,0,0,604,1,916,1,7539,1); //birds nest
	Add(1,5346,1,0,0,5100,1,7112,25,558,1,7539,1); //gf recruter hat
	Add(1,5243,1,0,0,7446,1,2339,10,1059,70,7539,1);//shafka
	Add(1,5375,1,0,0,5094,1,10018,2,968,5,7440,30,7539,2); //orc boss hat 
	Add(1,5068,1,20000,0,2255,1,2286,1,7539,1); //evil wing ears
	Add(1,5016,1,0,0,1030,10,0,0,0,0,0,0); //boys cap
	Add(1,5056,1,0,0,5079,1,578,100,7539,1); //fruit of love
	Add(1,5139,1,0,0,5085,1,710,5,7510,1,975,1,7539,1); //hibiscus
	Add(1,5115,1,50000,0,983,1,7267,99,749,1,7539,1); //winter hat
	Add(1,5342,1,0,0,5137,1,5040,1,1020,20,7152,5,7539,1); //sorin doll hat
	Add(1,5352,1,0,0,5035,1,979,1,938,100,7539,1); //poporing cap
	Add(1,5418,1,0,0,5016,1,2261,1,968,1,975,5,1004,35,7539,1);//soldier hat
	Add(1,5059,1,0,0,5030,1,7213,100,7217,100,7161,300,7539,1); //teddybear hat
	Add(1,5229,1,0,0,1038,600,7048,40,982,1,983,1,7539,1); //brown deviruchi hat
	Add(1,5448,1,0,0,521,200,1019,300,7539,1); //indifferent solo hat
	Add(1,5354,1,0,0,5431,1,12080,1,1629,1,7539,1); //muslim hat
	Add(1,5214,1,0,0,976,1,1060,300,1022,100,5008,5,7539,1); //moonlight flower hat
	Add(1,5171,1,0,0,4219,1,4114,1,4177,1,4259,1,4212,1,4073,1,4112,1,4081,1,4251,1,4166,1,7511,1000,7563,1000,7539,7); //valkyrie helm
	Add(1,18507,1,0,0,1040,200,993,20,997,20,969,5,1005,1,2286,1,7539,7); //slotted elven ears
	Add(1,18539,1,2000000,0,5184,1,5017,1,7005,100,983,10,7539,9); //skull cap
	Add(1,5013,1,50000000,0,754,100,753,100,752,100,751,100,750,100,743,100,742,100,741,100,2613,100,5053,100,7020,100,7036,100,2268,100,734,100,923,100,968,100,7005,100,714,300,7539,15);
	Add(1,5273,1,0,0,2261,1,914,200,982,1,975,1,979,1,7539,4); //Happy Wig

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1