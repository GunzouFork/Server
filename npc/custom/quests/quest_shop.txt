//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Costumes [^ff0000SOON^000000]","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5117,1,50000,0,731,10,748,2,982,1,7539,1); //mystic rose
	Add(1,5389,1,0,0,5043,5,969,10,7568,50,7539,8); //angel spirit
	Add(1,5315,1,0,0,2276,1,5135,1,7094,50,7353,50,7539,1); //observer
	Add(1,5418,1,0,0,5016,1,2261,1,968,1,975,5,1004,35,7539,1);//soldier hat
	Add(1,5100,1,0,0,2272,1,7451,4,7433,3,690,10,7539,1); //sales banner
	Add(1,5213,1,0,0,2214,1,983,1,7063,30,7539,1); //shiela hairnet
	Add(1,5102,1,0,0,7263,2,2276,1,2239,1,568,150,7539,1); //blank eye
	Add(1,5028,1,0,0,2279,1,7035,50,526,100,7539,1); //candle
	Add(1,5077,1,0,0,2278,1,975,1,7539,1); //tulip hairpin
	Add(1,5240,1,0,0,2252,1,1036,400,7001,50,4052,1,982,1,983,1,7539,1); //brown mage hat
	Add(1,5361,1,0,0,7216,200,7539,7); //gangster scarf
	Add(1,5237,1,0,0,2227,1,7038,500,975,1,982,1,7539,1); //pink beanie
	Add(1,5065,1,0,0,624,1,959,300,544,50,1023,1,7539,1); //blue fish
	Add(1,5292,1,0,0,7266,2,1035,4,10001,1,7539,1); //dragon skull
	Add(1,5075,1,0,0,2248,1,7030,108,7194,108,7120,4,7539,1); //cow boy hat
	Add(1,18507,1,0,0,1040,200,993,20,997,20,969,5,1005,1,2286,1,7539,7); //slotted elven ears
	Add(1,5238,1,0,0,2252,1,1036,400,7001,50,4052,1,975,1,7539,1); //red mage hat
	Add(1,5258,1,0,0,2261,1,914,200,975,1,976,1,978,1,7539,1); //afro wig
	Add(1,5110,1,0,0,526,2,7270,1,941,1,10004,1,7539,1); //baby pacifier
	Add(1,5074,1,20000,0,2286,1,2254,1,7539,1); // angel wing ears
	Add(1,5147,1,0,0,2227,1,978,1,1057,100,7539,1); //baseball cap
	Add(1,5806,1,0,0,2280,1,7479,1,2109,1,7539,1); //poen natalia hat
	Add(1,5449,1,0,0,521,200,1019,300,7539,1); //angry solo hat
	Add(1,5228,1,0,0,1038,600,7048,40,982,1,7539,1); //gray deviruchi hat
	Add(1,5198,1,0,0,5058,1,2214,1,949,500,914,200,622,50,1974,1,7539,1); //evolve drooping bunny
	Add(1,5338,1,0,0,5100,1,7112,25,561,1,7539,1); //bf recruter hat
	Add(1,5368,1,0,0,5197,6,7751,25,561,3,2210,1,7063,20,7539,1); //white wing
	Add(1,5107,1,0,0,519,50,7031,50,548,50,539,50,7539,1); //crunch toast
	Add(1,5176,1,0,0,706,1,1019,500,7200,10,7015,20,2267,1,952,100,1028,100,7539,1); //hahoe mask
	Add(1,5352,1,0,0,5035,1,979,1,938,100,7539,1); //poporing cap
	Add(1,18600,1,0,0,5184,1,5057,1,7539,9); //cat ear beret
	Add(1,5303,1,0,0,712,100,710,5,629,5,10009,1,7539,1); //flower crown
	Add(1,2273,1,3500,0,2275,1,998,50,733,1,7539,1); //doctor band
	Add(1,5144,1,0,0,2214,1,2221,1,7567,25,1001,200,7539,1); //gamble hat
	Add(1,5385,1,0,0,753,100,7539,1); //yoyo hat
	Add(1,5191,1,0,0,2208,1,983,1,7539,1); //black ribbon
	Add(1,2296,1,50000,0,2243,1,999,100,7539,1); //binoculars
	Add(1,5033,1,0,0,1036,20,2213,1,7065,300,7012,200,7539,1); //raccoon hat
	Add(1,5049,1,0,0,1099,1500,7539,1); //stripped hairband
	Add(1,5305,1,0,0,1214,5,969,10,7799,5,7539,6); //pirate dagger
	Add(1,5277,1,0,0,5278,1,508,200,7539,1); //Air pirates bandana
	Add(1,5371,1,0,0,5016,1,1004,35,7539,1); //judge hat
	Add(1,5417,1,0,0,4112,1,5299,1,7539,1); //crown parrot
	Add(1,5256,1,0,0,7510,1,2210,1,7063,50,7539,1); //valkyrie featherband
	Add(1,5324,1,0,0,5137,1,7063,20,1034,20,7539,1); //little angel doll
	Add(1,5079,1,0,0,2294,1,7220,400); //x-hairpin
	Add(1,5345,1,0,0,5137,1,5195,2,1060,45,7539,1); //w doll hat
	Add(1,5314,1,0,0,7567,200,5102,1,7539,1); //hockey mask

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(4,531,1,3,0,512,1,713,1);
	Add(4,532,1,3,0,513,1,713,1);
	Add(4,533,1,3,0,514,1,713,1);
	Add(4,534,1,3,0,515,1,713,1);
	
// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1