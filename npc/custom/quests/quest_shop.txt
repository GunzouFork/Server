//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5043,1,0,0,2281,1,1048,50,7539,1); //opera phantom mask
	Add(1,5137,1,0,0,5141,5,7047,100,661,1,7539,2); //alice doll
	Add(1,5146,1,0,0,978,1,949,500,1019,1,7539,1); //elephant hat
	Add(1,5070,1,0,0,7216,300,7097,300,2211,1,982,1,7539,1); //hot blooded headband
	Add(1,5196,1,0,0,2208,1,980,1,7539,1); //orange ribbon
	Add(1,5324,1,0,0,5137,1,7063,20,1034,20,7539,1); //little angel doll
	Add(1,5212,1,0,0,1820,2,5011,1,2651,1,4125,1,7539,1); //satellite hairband
	Add(1,5342,1,0,0,5137,1,5040,1,1020,20,7152,5,7539,1); //sorin doll hat
	Add(1,5071,1,10000,0,5010,1,5049,1,7101,10,7539,1); //indian headband
	Add(1,5333,1,0,0,996,4,12028,1,12032,1,992,10,5011,1,7539,1); //radio anthena
	Add(1,5039,1,0,0,7030,50,978,1,5015,1,7539,1); //rainbow eggshell
	Add(1,5364,1,0,0,5347,1,975,1,982,1,983,1,5048,1,7539,1); //evil snake lord hat    
	Add(1,2281,1,5000,0,998,20,707,1,7539,1); //opera masque
	Add(1,5239,1,0,0,2252,1,1036,400,7001,50,4052,1,982,1,7539,1); //gray mage hat
	Add(1,5206,1,0,0,2269,1,982,1,7539,1); //romantic white flower
	Add(1,5518,1,50000000,0,6004,1,7799,50,7798,30,2256,1,7539,10); //large magestic goat
	Add(1,5068,1,20000,0,2255,1,2286,1,7539,1); //evil wing ears
	Add(1,5800,1,0,0,5040,1,7170,1,2371,1,7539,6); //blush of groom
	Add(1,5156,1,0,0,2265,10,982,1,7539,1); //fater's mask
	Add(1,5299,1,0,0,7441,20,949,50,916,15,7115,10,7440,20,7063,10,7200,10,2214,1,7539,1); //pagdayaw
	Add(1,5289,1,0,0,526,200,7268,1,7539,1); //vanilmirth hat
	Add(1,5108,1,5000,0,7301,1887,5114,1,611,10,7539,1); //detective cap
	Add(1,5205,1,0,0,5185,1,610,100,706,25,7194,113,7539,1); //emperor laurer crown
	Add(1,5369,1,0,0,5191,6,7205,25,558,3,2210,1,7441,5,7539,1); //dark wing
	Add(1,5049,1,0,0,1099,1500,7539,1); //stripped hairband
	Add(1,5140,1,0,0,5085,1,2244,1,5191,1,7166,50,7539,1); //charming ribbon
	Add(1,5144,1,0,0,2214,1,2221,1,7567,25,1001,200,7539,1); //gamble hat
	Add(1,5233,1,0,0,2233,1,983,1,7206,300,7030,1,978,1,7539,1); //blue drooping cat
	Add(1,5424,1,0,0,980,1,620,1,605,50,985,200,7539,1); //fanta orange can hat
	Add(1,5098,1,0,0,1029,10,2297,5,7267,50,7539,1); //tiger mask
	Add(1,5423,1,5000000,0,736,300,5074,1,994,1,7539,8); // i love china
	Add(1,2214,1,0,0,949,100,706,1,722,1,2213,1,7539,1); //bunny band
	Add(1,5277,1,0,0,5278,1,508,200,7539,1); //Air pirates bandana
	Add(1,5397,1,0,0,7325,5,2205,1,7355,1,7539,1); //scuba mask
	Add(1,5252,1,0,0,5058,1,1022,250,7267,100,7539,1); //drooping 9tails
	Add(1,5181,1,0,0,7511,100,7340,200,7023,10,7539,1); //helm of darkness
	Add(1,2273,1,3500,0,2275,1,998,50,733,1,7539,1); //doctor band
	Add(1,5376,1,0,0,641,1,2210,1,7063,60,983,4,7539,3); //flying evil wing	
	Add(1,5255,1,0,0,5153,1,741,10,5132,1,5074,1,7539,1); //triple poring hat
	Add(1,5051,1,0,0,7047,100,7539,1); //pussy cat bell
	Add(1,5048,1,0,0,5041,1,999,10,7539,1); //cresent hairpin
	Add(1,5203,1,0,0,2278,1,1015,1,999,5,7539,1); //smiling mask
	Add(1,5176,1,0,0,706,1,1019,500,7200,10,7015,20,2267,1,952,100,1028,100,7539,1); //hahoe mask
	Add(1,5430,1,50000000,0,5414,1,7135,10,7451,4,7539,1); //promethean crown
	Add(1,5339,1,0,0,5100,1,7112,25,1558,1,7539,1); //friend recruiter
	Add(1,5188,1,0,0,5027,1,7166,50,7064,1,7539,1); //creative convenient hat
	Add(1,5361,1,0,0,7216,200,7539,7); //gangster scarf	
	Add(1,5021,1,0,0,2233,1,969,1,999,20,949,80,938,800,7539,1); //grief of greed
	Add(1,5433,1,0,0,5185,1,969,2,7539,1); //champions,s wreath
	Add(1,5311,1,0,0,5139,5,7539,1); //large hibiscus

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1