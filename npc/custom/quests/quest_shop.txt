//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5409,1,0,0,5075,1,7101,5,981,10,7539,1); //purple cowboy hat
	Add(1,5001,1,0,0,999,40,984,1,970,1,1003,1,7539,1); //headset
	Add(1,5313,1,0,0,5124,1,2249,1,5007,1,523,200,7539,1); //diadem
	Add(1,5013,1,50000000,0,754,100,753,100,752,100,751,100,750,100,743,100,742,100,741,100,2613,100,5053,100,7020,100,7036,100,2268,100,734,100,923,100,968,100,7005,100,714,300,7539,15);
	Add(1,5133,1,0,0,5160,1,7106,6,7107,80,7539,1); //sheep hat
	Add(1,5196,1,0,0,2208,1,980,1,7539,1); //orange ribbon
	Add(1,5392,1,100000,0,721,5,7216,25,7539,1); //louyang hat
	Add(1,5076,1,0,0,2227,1,7038,500,7539,1); //beanie
	Add(1,5286,1,0,0,2254,2,7101,200,2210,2,7510,10,7539,4); //pecopeco hairband
	Add(1,5289,1,0,0,526,200,7268,1,7539,1); //vanilmirth hat
	Add(1,5231,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,983,1,7539,1); //brown drooping cat
	Add(1,5247,1,0,0,1039,60,923,25,2255,1,7539,1); //satanic helm
	Add(1,2280,1,10000,0,1019,120); //sakkat
	Add(1,5099,1,0,0,7267,300,7171,300,1029,5,7539,1); //neko mini
	Add(1,5270,1,0,0,7198,200,7097,40,7539,1); //autum leaves
	Add(1,5304,1,0,0,7017,2,2510,1,983,1,7539,1); //cap of blindness
	Add(1,5396,1,0,0,5430,1,5285,1,7262,20,5396,1,7539,1); //jasper crest
	Add(1,5360,1,0,0,5057,1,2747,2,5161,1,7539,1); //wickebine ears
	Add(1,5448,1,0,0,521,200,1019,300,7539,1); //indifferent solo hat
	Add(1,5050,1,0,0,5037,1,7064,500,7539,1); //wonder nut shell
	Add(1,5210,1,0,0,2254,10,7063,50,7168,200,7038,200,7539,1); //flapping angel wing
	Add(1,5103,1,0,0,5351,1,10011,1,7539,1); //sunflower ahirpin
	Add(1,2292,1,2000,0,999,50,7539,1); //welding mask
	Add(1,5800,1,0,0,5040,1,7170,1,2371,1,7539,6); //blush of groom
	Add(1,5397,1,0,0,7325,5,2205,1,7355,1,7539,1); //scuba mask
	Add(1,5424,1,0,0,980,1,620,1,605,50,985,200,7539,1); //fanta orange can hat
	Add(1,5384,1,0,0,5390,1,975,1,7539,1); //twin pom santa
	Add(1,5365,1,0,0,574,150,7031,150,7539,1); //mangolia hat
	Add(1,5105,1,0,0,2288,1,1095,3000,7539,1); //2nd aniversary hat
	Add(1,18539,1,2000000,0,5184,1,5017,1,7005,100,983,10,7539,9); //skull cap
	Add(1,5335,1,0,0,741,5,607,5,940,150,7539,1); //jumping poring
	Add(1,2272,1,911000,0,1019,50,983,1,7539,1); //stop post
	Add(1,5169,1,0,0,706,1,1019,500,7200,10,1049,20,7165,500,1054,2,1024,100,7539,1); //bride mask
	Add(1,5412,1,0,0,530,20,7035,10,7539,1); //lollipop
	Add(1,5184,1,0,0,5120,5,995,50,983,1,720,50,7539,1); //captains hat
	Add(1,5262,1,0,0,5164,1,7799,1,7578,1,969,3,7539,1); //golden tiara
	Add(1,5021,1,0,0,2233,1,969,1,999,20,949,80,938,800,7539,1); //grief of greed
	Add(1,5301,1,0,0,5191,3,5195,3,7539,1); //victorious hairband
	Add(1,5358,1,0,0,2286,1,7101,30,7539,1); //peco ears
	Add(1,5192,1,0,0,2208,1,976,1,7539,1); //yellow ribbon
	Add(1,5200,1,0,0,5016,1,981,1,7317,20,7539,1); //coppola
	Add(1,5368,1,0,0,5197,6,7751,25,561,3,2210,1,7063,20,7539,1); //white wing
	Add(1,5341,1,80000000,0,5137,1,2276,1,1060,25,7539,1); //glaris doll hat
	Add(1,5186,1,0,0,1032,100,7100,100,905,250,7539,1); //geographer band
	Add(1,5022,1,0,0,7086,1,969,10,999,40,1003,50,984,2,7539,1); //hat of the sun
	Add(1,5040,1,0,0,7047,100,7539,1); //blush
	Add(1,5029,1,0,0,7068,300,7033,850,1015,1,7539,1); //spore hat
	Add(1,5100,1,0,0,2272,1,7451,4,7433,3,690,10,7539,1); //sales banner
	Add(1,5431,1,50000000,0,5283,1,916,50,2717,1,7539,1); //chicken hat
	Add(1,2214,1,0,0,949,100,706,1,722,1,2213,1,7539,1); //bunny band
	Add(1,18600,1,0,0,5184,1,5057,1,7539,9); //cat ear beret
	Add(1,5183,1,0,0,604,100,916,100,7115,100,7539,1); //bird nest hat
	Add(1,5185,1,0,0,5061,1,712,100,7194,150,7298,100,520,50,521,50,7539,1); //laurel wreath
	Add(1,5332,1,0,0,7019,10,7200,10,7539,1); //loki mask
	Add(1,5234,1,0,0,2233,1,983,1,7206,300,7030,1,976,1,7539,1); //yellow drooping cat

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1