//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Costumes","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5031,1,0,0,5009,1,5028,1,747,1,999,25,7539,1); //mine hat
	Add(1,5318,1,0,0,5035,2,5060,2,741,10,530,50,938,100,7539,1); //poring party hat
	Add(1,5297,1,0,0,5257,1,1472,2,5158,2,7063,40,7539,1); //soul wing
	Add(1,5091,1,20000,0,10016,1,714,1,969,3,7539,1); //decorative bell
	Add(1,5311,1,0,0,5139,5,7539,1); //large hibiscus
	Add(1,5421,1,100000000,0,2286,1,7122,60,7539,1); //ifrit ears
	Add(1,5282,1,0,0,5200,1,982,1,978,2,7539,1); //europe baseball cap
	Add(1,5413,1,0,0,608,20,1752,1,2261,1,978,1,7539,1); //popcorn hat
	Add(1,5139,1,0,0,5085,1,710,5,7510,1,975,1,7539,1); //hibiscus
	Add(1,5027,1,0,0,2252,1,1036,400,7001,50,4052,1,7539,1); //mage hat
	Add(1,5036,1,0,0,2608,1,7069,500,7539,1); //cross hat
	Add(1,5179,1,0,0,2234,1,969,3,2610,4,13106,2,7539,1); //golden tiara
	Add(1,5357,1,0,0,7510,20,7539,1); //wing of victory
	Add(1,5429,1,0,0,5100,1,1021,30,7268,1,7539,1); //dokeibi hat
	Add(1,5354,1,0,0,5431,1,12080,1,1629,1,7539,1); //muslim hat
	Add(1,5344,1,0,0,5137,1,7038,20,1060,25,7539,1); //bennit doll
	Add(1,5317,1,0,0,5120,1,2290,1,938,4,7539,1); //Fisherman's Hat
	Add(1,5229,1,0,0,1038,600,7048,40,982,1,983,1,7539,1); //brown deviruchi hat
	Add(1,5063,1,0,0,970,1,930,500,7539,1); //giant bandaid
	Add(1,5194,1,0,0,2208,1,975,1,982,1,7539,1); //pink ribbon
	Add(1,5070,1,0,0,7216,300,7097,300,2211,1,982,1,7539,1); //hot blooded headband
	Add(1,5232,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,975,1,7539,1); //pink drooping cat
	Add(1,5422,1,500000,0,1550,3,1006,20,7539,1); //linguistic book hat
	Add(1,5187,1,0,0,5085,1,5091,1,5117,2,978,1,975,1,7539,1); //twin red ribbon
	Add(1,5380,1,0,0,5065,2,7539,1); //fish head hat
	Add(1,5203,1,0,0,2278,1,1015,1,999,5,7539,1); //smiling mask
	Add(1,5031,1,0,0,7013,1200,7539,1); //mine hat
	Add(1,5310,1,0,0,5034,1,1971,2,996,6,7539,1); //shiny electric bulb
	Add(1,5333,1,0,0,996,4,12028,1,12032,1,992,10,5011,1,7539,1); //radio anthena
	Add(1,5343,1,0,0,5137,1,2215,1,1034,25,7539,1); //Telling doll hat
	Add(1,5388,1,0,0,1048,50,926,50,7539,1); //snake hat 
	Add(1,5307,1,0,0,2275,5,514,2,979,1,634,1,619,20,1027,20,7539,1); //carmen miranda hat
	Add(1,5048,1,0,0,5041,1,999,10,7539,1); //cresent hairpin
	Add(1,5340,1,0,0,5137,1,5040,1,1034,25,7539,1); //defolty doll hat      
	Add(1,5024,1,0,0,529,10,530,5,539,20,999,10,538,15,7539,1); //cake
	Add(1,5084,1,0,0,1026,1000,7065,100,945,100,7030,1,7539,1); //lazy smokie
	Add(1,5073,1,0,0,2285,1,1550,1,7539,1); //model training hat
	Add(1,5043,1,0,0,2281,1,1048,50,7539,1); //opera phantom mask
	Add(1,5803,1,0,0,2215,3,2210,2,5061,1,10009,1,7539,1); //love flower hat
	Add(1,5359,1,0,0,2284,1,5019,1,975,1,7539,7); //ship captain hat
	Add(1,5278,1,0,0,2210,1,5192,1,2250,1,7539,1); //yellow ribbon
	Add(1,5212,1,0,0,1820,2,5011,1,2651,1,4125,1,7539,1); //satellite hairband
	Add(1,2284,1,0,0,923,20); //antler
	Add(1,5094,1,0,0,968,100,2299,1,1124,1,931,1000,7539,2); //orc hero helm
	Add(1,5023,1,0,0,1059,150,907,100,978,1,7539,1); //parcel hat
	Add(1,5443,1,0,0,916,500,523,1,7292,10,2282,1,640,1,7539,1); //Statue Of Baby Angel
	Add(1,2272,1,911000,0,1019,50,983,1,7539,1); //stop post
	Add(1,5099,1,0,0,7267,300,7171,300,1029,5,7539,1); //neko mini
	Add(1,5022,1,0,0,7086,1,969,10,999,40,1003,50,984,2,7539,1); //hat of the sun
	Add(1,5029,1,0,0,7068,300,7033,850,1015,1,7539,1); //spore hat
	Add(1,5423,1,5000000,0,736,300,5074,1,994,1,7539,8); // i love china
	Add(1,5274,1,0,0,2261,1,914,200,980,2,975,1,7539,4); //shiny wig
	Add(1,5805,1,0,0,5161,1,7539,15); //victorious coronet
	Add(1,5376,1,0,0,641,1,2210,1,7063,60,983,4,7539,3); //flying evil wing
	Add(1,5423,1,5000000,0,736,300,5074,1,994,1,7539,8); // i love china

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,30032,1,0,0,5746,1);
	Add(3,30033,1,0,0,5747,1);
	Add(3,30034,1,0,0,5748,1);
	Add(3,30035,1,0,0,5749,1);
	Add(3,30036,1,0,0,5750,1);
	Add(3,30037,1,0,0,5751,1);
	Add(3,30038,1,0,0,5752,1);
	Add(3,30039,1,0,0,5753,1);
	Add(3,30040,1,0,0,5754,1);
	Add(3,30041,1,0,0,5755,1);
	Add(3,30042,1,0,0,5756,1);
	Add(3,30043,1,0,0,5757,1);
	Add(3,30044,1,0,0,5758,1);
	Add(3,30045,1,0,0,5760,1);
	
	Add(4,531,1,3,0,512,1,713,1);
	Add(4,532,1,3,0,513,1,713,1);
	Add(4,533,1,3,0,514,1,713,1);
	Add(4,534,1,3,0,515,1,713,1);
	
// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1