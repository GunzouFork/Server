//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,2237,1,0,0,1020,100,983,1,2241,1,7539,1); //bandit beard
	Add(1,5801,1,0,0,5083,1,2700,12,7566,12,7562,12,2357,3,7539,1); //red tailed ribbon
	Add(1,5031,1,0,0,7013,1200,7539,1); //mine hat
	Add(1,5063,1,0,0,970,1,930,500,7539,1); //giant bandaid
	Add(1,5414,1,100000000,0,7300,9,1068,4,7098,2,7539,1); //campfire hat
	Add(1,5179,1,0,0,2234,1,969,3,2610,4,13106,2,7539,1); //golden tiara
	Add(1,2292,1,2000,0,999,50,7539,1); //welding mask
	Add(1,5359,1,0,0,2284,1,5019,1,975,1,7539,7); //ship captain hat
	Add(1,5098,1,0,0,1029,10,2297,5,7267,50,7539,1); //tiger mask
	Add(1,5413,1,0,0,608,20,1752,1,2261,1,978,1,7539,1); //popcorn hat
	Add(1,5449,1,0,0,521,200,1019,300,7539,1); //angry solo hat
	Add(1,5278,1,0,0,2210,1,5192,1,2250,1,7539,1); //yellow ribbon
	Add(1,5156,1,0,0,2265,10,982,1,7539,1); //fater's mask
	Add(1,5316,1,0,0,2226,1,622,10,7539,1); //umberalla hat
	Add(1,5369,1,0,0,5191,6,7205,25,558,3,2210,1,7441,5,7539,1); //dark wing
	Add(1,5209,1,0,0,5147,1,976,1,7539,1); //love dad cap
	Add(1,5234,1,0,0,2233,1,983,1,7206,300,7030,1,976,1,7539,1); //yellow drooping cat
	Add(1,5343,1,0,0,5137,1,2215,1,1034,25,7539,1); //Telling doll hat
	Add(1,5429,1,0,0,5100,1,1021,30,7268,1,7539,1); //dokeibi hat
	Add(1,5337,1,0,0,5100,1,7112,25,2617,1,7539,1); //party recruiter hat
	Add(1,5297,1,0,0,5257,1,1472,2,5158,2,7063,40,7539,1); //soul wing
	Add(1,5323,1,0,0,5081,3,7754,1,2128,1,7539,1); //misstrance crown
	Add(1,2278,1,0,0,705,10,909,10,914,10,7539,1); //mr. smile
	Add(1,2214,1,0,0,949,100,706,1,722,1,2213,1,7539,1); //bunny band
	Add(1,5388,1,0,0,1048,50,926,50,7539,1); //snake hat 
	Add(1,2240,1,0,0,1040,2,982,2,949,4,7457,1,7539,1); //grandpa beared
	Add(1,5040,1,0,0,7047,100,7539,1); //blush
	Add(1,5181,1,0,0,7511,100,7340,200,7023,10,7539,1); //helm of darkness
	Add(1,5324,1,0,0,5137,1,7063,20,1034,20,7539,1); //little angel doll
	Add(1,5001,1,0,0,999,40,984,1,970,1,1003,1,7539,1); //headset
	Add(1,5292,1,0,0,7266,2,1035,4,10001,1,7539,1); //dragon skull
	Add(1,5105,1,0,0,2288,1,1095,3000,7539,1); //2nd aniversary hat
	Add(1,5210,1,0,0,2254,10,7063,50,7168,200,7038,200,7539,1); //flapping angel wing
	Add(1,5372,1,0,0,5099,1,5230,1,2747,5,982,15,7539,1); //koneko hat
	Add(1,5185,1,0,0,5061,1,712,100,7194,150,7298,100,520,50,521,50,7539,1); //laurel wreath
	Add(1,5294,1,0,0,7019,5,7567,50,7539,1); //wisper mask
	Add(1,5018,1,500,0,2247,1,916,300,7539,1); //feather bonnet
	Add(1,5332,1,0,0,7019,10,7200,10,7539,1); //loki mask
	Add(1,5358,1,0,0,2286,1,7101,30,7539,1); //peco ears
	Add(1,5207,1,0,0,2282,5,2254,2,969,1,7063,15,7539,1); //angel blessing
	Add(1,5411,1,0,0,5388,1,982,5,7263,2,978,2,7539,1); //white snake hat
	Add(1,5149,1,0,0,2234,1,714,5,2611,25,7539,1); //silver tiera
	Add(1,5081,1,40000,0,2249,1,714,1,969,3,7539,1); //mistress crown
	Add(1,5433,1,0,0,5185,1,969,2,7539,1); //champions,s wreath
	Add(1,5026,1,0,0,1036,450,949,330,539,120,982,1,7539,1); //chef hat
	Add(1,5420,1,300000000,0,5430,2,5421,1,7539,1); //ifrit mask
	Add(1,5086,1,0,0,5024,1,539,30,660,3,7539,1); //alarm mask
	Add(1,5282,1,0,0,5200,1,982,1,978,2,7539,1); //europe baseball cap
	Add(1,5137,1,0,0,5141,5,7047,100,661,1,7539,2); //alice doll
	Add(1,5034,1,0,0,2233,1,746,20,7539,1); //bulb band
	Add(1,5393,1,777777,0,2285,5,994,10,7097,25,7539,6); //vallentine hat
	Add(1,5155,1,0,0,7024,10,7754,2,7562,4,7450,1,7539,10); //father's white moustache
	Add(1,5377,1,100000000,0,2268,1,1040,30,1629,1,7539,7); //gentleman pipe
	Add(1,5286,1,0,0,2254,2,7101,200,2210,2,7510,10,7539,4); //pecopeco hairband
	Add(1,5800,1,0,0,5040,1,7170,1,2371,1,7539,6); //blush of groom

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1