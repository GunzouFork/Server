//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5102,1,0,0,7263,2,2276,1,2239,1,568,150,7539,1); //blank eye
	Add(1,5236,1,0,0,2227,1,7038,500,978,1,7539,1); //blue beanie
	Add(1,5022,1,0,0,7086,1,969,10,999,40,1003,50,984,2,7539,1); //hat of the sun
	Add(1,5094,1,0,0,968,100,2299,1,1124,1,931,1000,7539,2); //orc hero helm
	Add(1,5254,1,0,0,5132,1,5068,1,2255,1,7023,5,983,1,7539,1); //deviling hat
	Add(1,5285,1,0,0,7192,200,10011,1,7539,1); //vane hairpin
	Add(1,5083,1,0,0,2244,1,2209,1,10007,1,7539,1); //red ribbon
	Add(1,5147,1,0,0,2227,1,978,1,1057,100,7539,1); //baseball cap
	Add(1,5405,1,0,0,7441,45,949,80,916,100,7539,1); //filir hat
	Add(1,5811,1,0,0,985,100,2241,10,7539,1); //santa beared
	Add(1,5033,1,0,0,1036,20,2213,1,7065,300,7012,200,7539,1); //raccoon hat
	Add(1,5020,1,0,0,2215,1,2210,1,7063,20,7539,1); //kafra band
	Add(1,5227,1,0,0,1038,600,7048,40,975,1,7539,1); //red deviruchi hat
	Add(1,2284,1,0,0,923,20); //antler
	Add(1,5177,1,0,0,706,1,1019,500,7200,10,1049,500,1053,2,980,1,7539,1); //mythical lion mask
	Add(1,5270,1,0,0,7198,200,7097,40,7539,1); //autum leaves
	Add(1,5067,1,0,0,5062,1,952,50,1907,1,7539,1); //sumbrero
	Add(1,5382,1,0,0,969,8,10016,2,5083,1,7539,1); //bell ribbon
	Add(1,5258,1,0,0,914,100,975,1,976,1,978,1,7539,1); //afro wig
	Add(1,5099,1,0,0,7267,300,7171,300,1029,5,7539,1); //neko mini
	Add(1,5239,1,0,0,2252,1,1036,400,7001,50,4052,1,982,1,7539,1); //gray mage hat
	Add(1,5289,1,0,0,526,200,7268,1,7539,1); //vanilmirth hat
	Add(1,5235,1,0,0,2227,1,7038,500,982,1,983,1,7539,1); //brown beanie
	Add(1,5262,1,0,0,5164,1,7799,1,7578,1,969,3,7539,1); //golden tiara
	Add(1,5036,1,0,0,2608,1,7069,500,7539,1); //cross hat
	Add(1,5427,1,0,0,5288,1,12078,1,7521,1,12183,1,7539,1); //black tea kochakaden hat
	Add(1,5241,1,0,0,2252,1,1036,400,7001,50,4052,1,978,1,7539,1); //blue mage hat
	Add(1,5052,1,0,0,2211,1,978,1,7003,300,7539,1); //blue hairband
	Add(1,5183,1,0,0,604,100,916,100,7115,100,7539,1); //bird nest hat
	Add(1,5380,1,0,0,5065,2,7539,1); //fish head hat
	Add(1,5806,1,0,0,2280,1,7479,1,2109,1,7539,1); //poen natalia hat
	Add(1,5211,1,0,0,5045,1,5083,1,7063,40,7553,1,982,1,975,1,7539,2); //dress hat
	Add(1,5385,1,0,0,753,100,7539,1); //yoyo hat
	Add(1,5345,1,0,0,5137,1,5195,2,1060,45,7539,1); //w doll hat
	Add(1,5080,1,0,0,10006,1,714,1,969,3,7539,1); //crown of ancient queen
	Add(1,5301,1,0,0,5191,3,5195,3,7539,1); //victorious hairband
	Add(1,5195,1,0,0,2208,1,975,1,7539,1); //red ribbond
	Add(1,5269,1,0,0,5203,4,7063,60,2210,1,7539,1); //flapping angel wing
	Add(1,5392,1,100000,0,721,5,7216,25,7539,1); //louyang hat
	Add(1,5315,1,0,0,2276,1,5135,1,7094,50,7353,50,7539,1); //observer
	Add(1,5363,1,0,0,5120,1,2261,1,983,20,7539,1); //abyssmal knight helm
	Add(1,5048,1,0,0,5041,1,999,10,7539,1); //cresent hairpin
	Add(1,5258,1,0,0,2261,1,914,200,975,1,976,1,978,1,7539,1); //afro wig
	Add(1,5260,1,0,0,4293,1,538,100,530,100,529,50,7539,1); //cookie hat
	Add(1,5284,1,0,0,5302,1,975,1,982,1,5165,1,7539,1); //water lily crown
	Add(1,5193,1,0,0,2208,1,979,1,7539,1); //green ribbon
	Add(1,5242,1,0,0,2252,1,1036,400,7001,50,4052,1,976,1,7539,1); //yellow mage hat
	Add(1,5197,1,0,0,2208,1,982,1,7539,1); //white ribbon
	Add(1,5803,1,0,0,2215,3,2210,2,5061,1,10009,1,7539,1); //love flower hat
	Add(1,5336,1,0,0,5100,1,7112,25,714,1,7539,1); //guildmen recuiter hat 
	Add(1,5025,1,0,0,2229,1,2254,1,7036,5,7539,1); //angelhelm
	Add(1,5325,1,0,0,5102,1,7355,12,7095,4,7317,2,7539,8); //robo eye
	Add(1,5389,1,0,0,5043,5,969,10,7568,50,7539,8); //angel spirit
	Add(1,18600,1,0,0,5184,1,5057,1,7539,9); //cat ear beret
	Add(1,5276,1,0,0,2261,1,914,200,983,1,976,1,975,1,7539,4); //fantastic wig
	Add(1,5208,1,0,0,1550,1,1553,7,1556,7,1554,7,1557,7,1555,7,7015,30,7539,3); //rideword hat

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1