//===== Hercules Script ======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,139,226,6	script	Quest Shop#1	510,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Weapons","Other";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5369,1,0,0,5191,6,7205,25,558,3,2210,1,7441,5,7539,1); //dark wing
	Add(1,5054,1,0,0,2252,1,1054,450,943,1200,7539,1); //assasin mask
	Add(1,5253,1,0,0,1034,300,7445,30,5141,10,979,1,7539,1); //lif doll hat
	Add(1,5050,1,0,0,5037,1,7064,500,7539,1); //wonder nut shell
	Add(1,5334,1,0,0,5132,1,1971,20,7093,4,2659,1,7539,1); //flapping angeling
	Add(1,5036,1,0,0,2608,1,7069,500,7539,1); //cross hat
	Add(1,5193,1,0,0,2208,1,979,1,7539,1); //green ribbon
	Add(1,5138,1,0,0,5123,1,5027,1,7337,50,981,1,7539,1); //magic eye
	Add(1,5074,1,20000,0,2286,1,2254,1,7539,1); // angel wing ears
	Add(1,5025,1,0,0,2229,1,2254,1,7036,5,7539,1); //angelhelm
	Add(1,5236,1,0,0,2227,1,7038,500,978,1,7539,1); //blue beanie
	Add(1,5242,1,0,0,2252,1,1036,400,7001,50,4052,1,976,1,7539,1); //yellow mage hat
	Add(1,5313,1,0,0,5124,1,2249,1,5007,1,523,200,7539,1); //diadem
	Add(1,5320,1,0,0,5205,1,1009,50,2111,1,7539,1); //champion wreath
	Add(1,5372,1,0,0,5099,1,5230,1,2747,5,982,15,7539,1); //koneko hat
	Add(1,5232,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,975,1,7539,1); //pink drooping cat
	Add(1,5059,1,0,0,5030,1,7213,100,7217,100,7161,300,7539,1); //teddybear hat
	Add(1,5262,1,0,0,5164,1,7799,1,7578,1,969,3,7539,1); //golden tiara
	Add(1,5039,1,0,0,7030,50,978,1,5015,1,7539,1); //rainbow eggshell
	Add(1,5332,1,0,0,7019,10,7200,10,7539,1); //loki mask
	Add(1,5383,1,0,0,5200,1,979,1,7445,50,7539,1); //hunting cap
	Add(1,5102,1,0,0,7263,2,2276,1,2239,1,568,150,7539,1); //blank eye
	Add(1,5411,1,0,0,5388,1,982,5,7263,2,978,2,7539,1); //white snake hat
	Add(1,5033,1,0,0,1036,20,2213,1,7065,300,7012,200,7539,1); //raccoon hat
	Add(1,2293,1,0,0,1049,4,7539,1); //pretend murder
	Add(1,5018,1,500,0,2247,1,916,300,7539,1); //feather bonnet
	Add(1,5302,1,0,0,7553,2,982,1,7771,1,7539,1); //water liliy hat
	Add(1,5803,1,0,0,2215,3,2210,2,5061,1,10009,1,7539,1); //love flower hat
	Add(1,5207,1,0,0,2282,5,2254,2,969,1,7063,15,7539,1); //angel blessing
	Add(1,5304,1,0,0,7017,2,2510,1,983,1,7539,1); //cap of blindness
	Add(1,5142,1,0,0,5017,1,2229,1,5014,1,7209,100,7123,100,1036,100,979,1,7539,1); //cresent helm
	Add(1,5063,1,0,0,970,1,930,500,7539,1); //giant bandaid
	Add(1,5073,1,0,0,2285,1,1550,1,7539,1); //model training hat
	Add(1,5202,1,0,0,5035,1,1062,100,535,80,7539,1); //pumkin hat
	Add(1,5210,1,0,0,2254,10,7063,50,7168,200,7038,200,7539,1); //flapping angel wing
	Add(1,5187,1,0,0,5085,1,5091,1,5117,2,978,1,975,1,7539,1); //twin red ribbon
	Add(1,5358,1,0,0,2286,1,7101,30,7539,1); //peco ears
	Add(1,5231,1,0,0,2233,1,983,1,7206,300,7030,1,982,1,983,1,7539,1); //brown drooping cat
	Add(1,5282,1,0,0,5200,1,982,1,978,2,7539,1); //europe baseball cap
	Add(1,5418,1,0,0,5016,1,2261,1,968,1,975,5,1004,35,7539,1);//soldier hat
	Add(1,5301,1,0,0,5191,3,5195,3,7539,1); //victorious hairband
	Add(1,5084,1,0,0,1026,1000,7065,100,945,100,7030,1,7539,1); //lazy smokie
	Add(1,5131,1,0,0,5128,1,969,10,978,1,985,27,7539,1); //close helm
	Add(1,5200,1,0,0,5016,1,981,1,7317,20,7539,1); //coppola
	Add(1,5177,1,0,0,706,1,1019,500,7200,10,1049,500,1053,2,980,1,7539,1); //mythical lion mask
	Add(1,5020,1,0,0,2215,1,2210,1,7063,20,7539,1); //kafra band
	Add(1,5300,1,0,0,7106,2,2246,1,7263,1,7539,1); //bullock helm
	Add(1,5254,1,0,0,5132,1,5068,1,2255,1,7023,5,983,1,7539,1); //deviling hat
	Add(1,5285,1,0,0,7192,200,10011,1,7539,1); //vane hairpin
	Add(1,5227,1,0,0,1038,600,7048,40,975,1,7539,1); //red deviruchi hat
	Add(1,5389,1,0,0,5043,5,969,10,7568,50,7539,8); //angel spirit
	Add(1,5805,1,0,0,5161,1,7539,15); //victorious coronet
	Add(1,5518,1,50000000,0,6004,1,7799,50,7798,30,2256,1,7539,10); //large magestic goat
	Add(1,5155,1,0,0,7024,10,7754,2,7562,4,7450,1,7539,10); //father's white moustache
	Add(1,5379,1,0,0,5061,2,978,1,2270,2,5083,1,1059,150,7539,5); //Tam 

	Add(2,1224,1,0,0,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,7292,30,969,10,999,50,714,10);

	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1