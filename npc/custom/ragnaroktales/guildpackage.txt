//= Item Rewards NPC
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.8
//===== Description: =========================================
//= Useful for event rewards, when a player is not necessarily online.
//= Logging is available, if needed (holds 128 names).
//= Note: Unclaimed rewards are limited to 64 at a time.
//============================================================

prontera,143,281,5	script	Guild Pack Distributor	852,{

// --------------------- Config ---------------------
// Package format is "ID1,Count1,ID2,Count2,..."
// GM Access: Level required to open the GM menu.
// GM Delete: Level required to erase entries.
// GM Logging: Level required to manage logs.

	set .GMAccess,60;
	set .GMDelete,80;
	set .GMLogging,99;
	set .PackageCount,1;
	setarray .Package1[0],6173,1,14533,10,12210,5,7776,3,14240,1,14236,1,14237,1,14238,1,14239,1,13892,1,13891,1,13893,1,13894,1;

// --------------------------------------------------

	if (getgmlevel() >= .GMAccess) goto GM_Menu;
	mes "[Guild Package Distributor]";
	set .@i,0;
	while (.@i < getarraysize($gpack$)) {
		if (strcharinfo(0) == $gpack$[.@i]) goto GetGPack;
		set .@i, .@i+2; }
	mes "Your Guild Package has not yet arrived.";
	close;

GetGPack:
	if (((Weight*100)/MaxWeight) > 49) {
		mes "You are over the weight limit."; close; }
	if (!checkweight(5055,getarraysize(getd(".Package"+$gpack$[.@i+1])))) {
		mes "Clear space in your inventory."; close; }
	set .@j,0;
	while (.@j < getarraysize(getd(".Package"+$gpack$[.@i+1]))) {
		getitem getd(".Package"+$gpack$[.@i+1]+"["+.@j+"]"),getd(".Package"+$gpack$[.@i+1]+"["+(.@j+1)+"]");
		set .@j, .@j+2; }
	specialeffect2 248;
	deletearray $gpack$[.@i],2;
	mes "Here's your guild package.";
	mes "More power to your Guild!";
	close;

OnMinute00:
OnMinute30:
	if (!getarraysize($gpack$)) end;
	set .@i,0;
	while (.@i < getarraysize($gpack$)) {
		message $gpack$[.@i],"[Your Guild Package has arrived. Visit the Guild Package Distributor to claim it.]";
		sleep 10;
		set .@i, .@i+2; }
	end;

GM_Menu:
	mes "[Guild Package Distributor]";
	mes "What would you like to do?";
	next;
	switch(select(" ~ ^FF55FFPackage information^000000: ~ ^FF55FFList unclaimed rewards^000000: ~ ^00D900Give a reward^000000: ~ "+((getgmlevel()<.GMDelete)?"^777777":"^DE0000")+"Delete a reward^000000: ~ "+((getgmlevel()<.GMDelete)?"^777777":"^DE0000")+"Delete all unclaimed rewards^000000: ~ "+((getgmlevel()<.GMLogging)?"^777777":"^55AAFF")+"Manage logs^000000: ~ [Close]")) {
		case 1:
			mes "[Guild Package Distributor]";
			set .@j,1;
			while (getd(".Package"+.@j)) {
				mes "^660099Package " + .@j + ":^000000";
				set .@i,0;
				while (.@i < getarraysize(getd(".Package"+.@j))) {
					mes " ~ " + getd(".Package"+.@j+"["+(.@i+1)+"]") + "x " + getitemname(getd(".Package"+.@j+"["+(.@i)+"]"));
					set .@i, .@i+2; }
				set .@j, .@j+1; }
			next;
			goto GM_Menu;
		case 2:
			mes "[Guild Package Distributor]";
			mes "Used space: ^F52887" + (getarraysize($gpack$)/2) + "^000000/64";
			mes "--------------------------------";
			set .@i,0;
			while (.@i < getarraysize($gpack$)) {
				mes "^B041FF" + $gpack$[.@i] + "^000000: package #" + $gpack$[.@i+1];
				set .@i, .@i+2; }
			next;
			goto GM_Menu;
		case 3:
			mes "[Guild Package Distributor]";
			if (getarraysize($gpack$) > 127) {
				mes "No more names can be stored.";
				mes "Delete some values and try again.";
				next;
				goto GM_Menu; }
			mes "Input a name, then a package number.";
			input .@name$;
				query_sql "SELECT `char_id` FROM `char` WHERE `name` = '"+.@name$+"'",.@charid;
				if (!.@charid) { mes "The name is invalid."; next; goto GM_Menu; }
					else set .@charid,0;
			input .@gpackage,1,.PackageCount;
			next;
			mes "[Guild Package Distributor]";
			mes "Player: ^B041FF" + .@name$ + "^000000";
			mes "Package: ^B041FF#" + .@gpackage + "^000000";
			mes " ";
			mes "Are you sure?";
			if (select("Yes:No") == 2) { mes "Request cancelled."; next; goto GM_Menu; }
			setarray $gpack$[getarraysize($gpack$)],.@name$,.@gpackage;
			message .@name$,"[Your Guild Package has arrived. Visit the Guild Package Distributor to claim it.]";
			if ($gpacklog) {
				if (getarraysize($gpacklog1$) > 127) {
					deletearray $gpacklog1$[0],1;
					deletearray $gpacklog2$[0],1; }
				setarray $gpacklog1$[getarraysize($gpacklog1$)],strcharinfo(0);
				setarray $gpacklog2$[getarraysize($gpacklog2$)],.@name$; }
			mes "Reward added.";
			next;
			goto GM_Menu;
		case 4:
			mes "[Guild Package Distributor]";
			if (getgmlevel() < .GMDelete) {
				mes "You are not permitted to delete entries."; next; goto GM_Menu; }
			mes "Input a name to cancel a reward.";
			input .@name$;
			set .@i,0;
			while (.@i < getarraysize($gpack$)) {
				if ($gpack$[.@i] == .@name$) {
					deletearray $gpack$[.@i],2;
					mes "Name cleared."; next; goto GM_Menu; }
				set .@i, .@i+2; }
			mes "The name is invalid.";
			next;
			goto GM_Menu;
		case 5:
			mes "[Guild Package Distributor]";
			if (getgmlevel() < .GMDelete) {
				mes "You are not permitted to delete entries."; next; goto GM_Menu; }
			mes "^FF0000This action cannot be undone.^000000";
			mes "Are you sure?";
			mes " ";
			if (select("Yes:No") == 2) { mes "Request cancelled."; next; goto GM_Menu; }
			deletearray $gpack$[0],getarraysize($gpack$);
			mes "All entries cleared.";
			next;
			goto GM_Menu;
		case 6:
		Log_Menu:
			mes "[Guild Package Distributor]";
			if (getgmlevel() < .GMLogging) {
				mes "You are not permitted to manage logs."; next; goto GM_Menu; }
			mes "Logs are currently "+(($gpacklog)?"^00D900enabled^000000":"^FF0000disabled^000000")+".";
			next;
			switch(select(" ~ ^55AAFFView Logs^000000: ~ "+((!$gpacklog)?"^00D900Enable":"^777777Disable")+" logging^000000: ~ ^DE0000Delete all logs^000000: ~ [Go back]")) {
				case 1:
					mes "[Guild Package Distributor]";
					mes "Used space: ^F52887" + (getarraysize($gpacklog1$)) + "^000000/128";
					mes "--------------------------------";
					set .@i,0;
					while (.@i < getarraysize($gpacklog1$)) {
						mes "^B041FF" + $gpacklog1$[.@i] + "^000000: sent to ^55AAFF" + $gpacklog2$[.@i] + "^000000";
						set .@i, .@i+1; }
					next;
					goto Log_Menu;
				case 2:
					set $gpacklog, ((!$gpacklog)?1:0);
					goto Log_Menu;
				case 3:
					mes "[Guild Package Distributor]";
					mes "^FF0000This action cannot be undone.^000000";
					mes "Are you sure?";
					mes " ";
					if (select("Yes:No") == 2) { mes "Request cancelled."; next; goto Log_Menu; }
					deletearray $gpacklog1$[0],getarraysize($gpacklog1$);
					deletearray $gpacklog2$[0],getarraysize($gpacklog2$);
					mes "Logs cleared.";
					next;
					goto Log_Menu;
				case 4:
					next;
					goto GM_Menu;
			}
		case 7:
			close;
	}
}